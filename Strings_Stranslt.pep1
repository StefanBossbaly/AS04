;************************************************************************
;Replaces the chars in Sobject by looking at the even indexed chars in
;table as keys and the odd indexed chars as values.
;For example Stranslt("Hello Mom","HjMWmw") results in "jello Wow"
;Author: Stefan Bossbaly
;********  void Stranslt (char[] Sobject, char[] table)
;
           .GLOBAL  Stranslt
;-- Arguments -----------------------------------------------------------
Sobject:   .EQUATE  2            ;formal parameter address of Sobject
table:     .EQUATE  4            ;formal parameter address of table
;-- Local Data ----------------------------------------------------------
sLen:      .WORD    0            ;length of Sobject
tLen:      .WORD    0            ;length of table
index:     .WORD    0            ;index of outLp
index2:    .WORD    0            ;index of inLp
char:      .BYTE    0            ;used as a temporary storage of a char
;-- Entry Point ---------------------------------------------------------
Stranslt:  LDA      Sobject,s    ; ----
           STA      -2,s         ;
           SUBSP    2,i          ;
           CALL     Slength      ; sLen = Slength(Sobject)
           ADDSP    2,i          ;
           STX      sLen,d       ; ----
           LDA      table,s      ; ----
           STA      -2,s         ;
           SUBSP    2,i          ;
           CALL     Slength      ; sLen = Slength(table)
           ADDSP    2,i          ;
           STX      tLen,d       ; ----
           LDA      0,i          ;clear the a register
           LDX      0,i          ;load 0 into x register
           STX      index,d      ;index = 0
outLp:     CPX      sLen,d       ;compare index to sLen
           BRGE     end          ;if (index >= sLen) end loop
           LDBYTEA  Sobject,sxf  ;a register = Sobject[index]
           STBYTEA  char,d       ;char = Sobject[index]
           LDX      0,i          ;load 0 into x register
           STX      index2,d     ;index2 = 0
inLp:      CPX      tLen,d       ;compare index2 to tLen
           BRGE     endinLp      ;if (index2 >= tLen) end inLp
           LDBYTEA  table,sxf    ;a register = table[index2]
           CPA      char,d       ;compare Sobject[index] to table[index2]
           BREQ     trans        ;if (Sobject[index] == table[index2])
           ADDX     2,i          ;index += 2 (we only want to check the even numbers)
           STX      index2,d     ;
           BR       inLp         ;loop
trans:     ADDX     1,i          ;index2 + 1
           LDBYTEA  table,sxf    ;a register = table[index2 + 1]
           LDX      index,d      ;load index into x register
           STBYTEA  Sobject,sxf  ; Sobject[index] = table[index2 + 1]
endinLp:   LDX      index,d      ;
           ADDX     1,i          ;index++
           STX      index,d      ;
           BR       outLp        ;loop!
end:       RET0                  ;return to calling program
;=========================================================================